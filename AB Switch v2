desc:AB Switch v2 (smartin)
//tags: AB program comparison diff sum 
//author: smartin
//modified: 2023-06-03
/*

Quickly switch between stereo program comparisons. You can 
use numkeys 0-3 to switch the mode.

  Mode                  Trigger
  *******************   *******
  Program A only          0
  Program B only          1
  Diff A-B (zero test)    2
  Sum A+B                 3
  
Input:
  Program A L/R on Channels 1-2
  Program B L/R on Channels 3-4
Output:
  L/R on channels 1-2 only
  Channels 3-4 are zereoed after mixing

NOTE: If you are using effects built for 2 channels (which you probably are)
this needs to be the first effect in the chain. Channels 3-4 are zereoed
after mixing.

*/

slider1:0<0,3,1{A,B,Diff,Sum}>PGM
slider2:0<-15,15,0.01>Diff Trim (dB)

in_pin:Program A (L)
in_pin:Program A (R)
in_pin:Program B (L)
in_pin:Program B (R)
in_pin:Program C (L)
in_pin:Program C (R)
in_pin:Program D (L)
in_pin:Program D (R)
in_pin:Program E (L)
in_pin:Program E (R)
in_pin:Program F (L)
in_pin:Program F (R)
in_pin:Program G (L)
in_pin:Program G (R)

out_pin:Mix (L)
out_pin:Mix (R)

@init
  btrim=1;

@slider
  btrim=2 ^ (slider2/6);
  
@block
trigger & (2^0) ? (
  slider1=0;
) : trigger & (2^1) ? (
  slider1=1;
) : trigger & (2^2) ? (
  slider1=2;
) : trigger & (2^3) ? (
  slider1=3;
);

@sample
slider1==0 ? (
  // Program A (channels 1-2)
  out0=spl0;
  out1=spl1;
) : slider1==1 ? (
  // Program B (channels 3-4)
  out0=spl2;
  out1=spl3;
) : slider1==2 ? (
  // Diff (A-B)
  spl2=spl2*btrim;
  spl3=spl3*btrim;
  out0=spl0-spl2;
  out1=spl1-spl3;
) : slider1==3 ? (
  // Sum (A+B)
  out0=spl0+spl2;
  out1=spl1+spl3;
  out0=out0/2;
  out1=out1/2;
);

spl0=out0;
spl1=out1;
spl2=0;
spl3=0;
